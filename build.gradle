allprojects {
    project.group = "io.honeycomb"
    project.version = "0.2.0"
}

subprojects {
    version = rootProject.version

    apply plugin: "java"

    ext {
        versions = [
                opentelemetry         : "1.0.1",
                opentelemetryJavaagent: "1.0.1",
                bytebuddy             : "1.10.18",
                guava                 : "30.1-jre"
        ]
        versions.opentelemetryAlpha = "${versions.opentelemetry}-alpha"
        versions.opentelemetryJavaagentAlpha = "${versions.opentelemetryJavaagent}-alpha"

        deps = [
                bytebuddy           : dependencies.create(group: 'net.bytebuddy', name: 'byte-buddy', version: versions.bytebuddy),
                bytebuddyagent      : dependencies.create(group: 'net.bytebuddy', name: 'byte-buddy-agent', version: versions.bytebuddy),
                autoservice         : [
                        dependencies.create(group: 'com.google.auto.service', name: 'auto-service', version: '1.0-rc7'),
                        dependencies.create(group: 'com.google.auto', name: 'auto-common', version: '0.8'),
                        dependencies.create(group: 'com.google.guava', name: 'guava', version: "${versions.guava}"),
                ],
                autoValueAnnotations: "com.google.auto.value:auto-value-annotations:${versions.autoValue}",
        ]
    }

    repositories {
        maven {
            url = uri("https://oss.sonatype.org/content/repositories/snapshots")
        }
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(8)) // compile it to ensure runtime compatibility with Java 8
        }
        withJavadocJar()
        withSourcesJar()
    }

    plugins.withId("maven-publish") {

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                    afterEvaluate {
                        artifactId = project.findProperty("archivesBaseName") as String
                    }
                    versionMapping {
                        usage('java-api') {
                            fromResolutionOf('runtimeClasspath')
                        }
                        usage('java-runtime') {
                            fromResolutionResult()
                        }
                    }
                    pom {
                        name = 'Honeycomb OpenTelemetry Distribution for Java'
                        url = 'https://github.com/honeycombio/honeycomb-opentelemetry-java'
                        afterEvaluate {
                            description = project.description
                        }
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        developers {
                            developer {
                                id = 'Honeycomb'
                                name = 'Honeycomb'
                                email = 'support@honeycomb.io'
                                organization = 'Honeycomb'
                                organizationUrl = 'https://honeycomb.io'
                            }
                        }
                        scm {
                            url = 'https://github.com/honeycombio/honeycomb-opentelemetry-java'
                            connection = 'scm:git:git@github.com:honeycombio/honeycomb-opentelemetry-java.git'
                            developerConnection = 'scm:git:git@github.com:honeycombio/honeycomb-opentelemetry-java.git'
                        }
                    }
                }
            }
            repositories {
                maven {
                    def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
                    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                    credentials {
                        username = System.getenv("OSSRH_USERNAME")
                        password = System.getenv("OSSRH_PASSWORD")
                    }
                }
            }
        }

        if (!project.hasProperty("skip.signing")) {
            plugins.apply("signing")

            signing {
                def base64key = System.getenv("GPG_BASE64")
                def pw = System.getenv("GPG_PASSPHRASE")
                def key = ""

                if (base64key != null) {
                    key = new String(Base64.getDecoder().decode(base64key)).trim()
                }

                useInMemoryPgpKeys(key, pw)
                sign publishing.publications.mavenJava
            }
        }

    }
}
