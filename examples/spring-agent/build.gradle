plugins {
	id 'org.springframework.boot' version '2.5.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}

group = 'io.honeycomb.examples'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

ext {
	honeycombDistroVersion = '0.4.0';
}

configurations {
    agent
}

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	agent "io.honeycomb:honeycomb-opentelemetry-javaagent:${honeycombDistroVersion}:all"
	implementation ("io.honeycomb:honeycomb-opentelemetry-sdk:${honeycombDistroVersion}")

	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

task copyAgentJar(type: Copy) {
    from configurations.agent {
        include '**/honeycomb-opentelemetry-javaagent*.jar'
    }
    into "agent"
    rename { fileName -> "honey-javaagent.jar" }
}

compileJava.dependsOn copyAgentJar
bootRun.dependsOn copyAgentJar

bootRun.doFirst {
    jvmArgs("-javaagent:agent/honey-javaagent.jar")
}

clean.doFirst {
    delete "agent"
}

test {
	useJUnitPlatform()
}
